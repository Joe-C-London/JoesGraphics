import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext.kotlin_version = '2.2.20'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "com.github.ben-manes.versions" version "0.53.0"
    id "com.diffplug.spotless" version "8.0.0"
    id "jacoco"
}
ext {
    junit_version = "6.0.0"
    junit_platform_version = "6.0.0"
    awaitility_version = "4.3.0"
}

apply plugin: 'kotlin'

group 'com.joecollins'
version '1.0-SNAPSHOT'

allprojects {
    repositories {
        maven {
            url "https://repo.osgeo.org/repository/release/"
        }
        mavenCentral()
    }

    apply plugin: "com.diffplug.spotless"

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions.allWarningsAsErrors = true
    }

    spotless {
        kotlin {
            ktlint()
                .editorConfigOverride([
                    "max_line_length" : 2147483647,
                ])
        }
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jacoco {
    toolVersion = "0.8.7"
    reportsDirectory = file("$buildDir/jacocoReports")
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = file("${buildDir}/jacocoReports")
    }
}

kotlin {
    jvmToolchain(17)
}